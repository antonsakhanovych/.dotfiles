* Basic UI configuration

** Define variables
#+begin_src emacs-lisp :tangle ./init.el

  ;; Define font sizes
  (defvar my-config/default-font-size 120)
  (defvar my-config/fixed-pitch-font-size 140)
  (defvar my-config/variable-pitch-font-size 210)
#+end_src

** Basic UI
  #+begin_src emacs-lisp :tangle ./init.el
    (setq inhibit-startup-message t) ;; Disable default splash screen
    (scroll-bar-mode -1) ;; Disable scrollbar
    (tool-bar-mode -1) ;; Disable the toolbar
    (tooltip-mode -1) ;; Disable tooltips
    (set-fringe-mode 10) ;; Modify fringe sizes
    (menu-bar-mode -1) ;; Disable the menubar
    (setq visible-bell t) ;; Set visible bell 
    (setq max-mini-window-height 0.15) ;; Resizing minibuffer and echo area
#+end_src

** Numeration of lines
#+begin_src emacs-lisp :tangle ./init.el
  (global-display-line-numbers-mode t) ;; Enable numeration of lines
  (setq column-number-mode t) ;; Display column number in the mini-buffer
    ;; Disable numeration of lines in some modes
    (dolist (mode '(org-mode-hook
                    shell-mode-hook
                    term-mode-hook
                    eshell-mode-hook))
      (add-hook mode (lambda () (display-line-numbers-mode 0))))
#+end_src

** Keeping folders clean
#+begin_src emacs-lisp :tangle ./init.el
  (use-package no-littering)

  (setq auto-save-file-name-transforms
        `((".*" ,(no-littering-expand-var-file-name "auto-save/") t)))
#+end_src
* Font configuration
#+begin_src emacs-lisp :tangle ./init.el
  (set-face-attribute 'default nil :font "Fira Code Retina" :height my-config/default-font-size)

  (set-face-attribute 'fixed-pitch nil :font "Fira Code Retina" :height my-config/fixed-pitch-font-size)

  (set-face-attribute 'variable-pitch nil :font "Cantarell" :height my-config/variable-pitch-font-size :weight 'regular)

#+end_src

* Setting up package manager

** Package and sources
#+begin_src emacs-lisp :tangle ./init.el
  (require 'package)

  (setq package-archives '(("org" . "https://orgmode.org/elpa/")
                           ("melpa" . "https://melpa.org/packages/")
                           ("elpa" . "https://elpa.gnu.org/packages/")))

  (package-initialize)
  (unless package-archive-contents
    (package-refresh-contents))
  
#+end_src

** Initializing package manager on non-Linux platforms
#+begin_src emacs-lisp :tangle ./init.el
  (unless (package-installed-p 'use-package)
    (package-install 'use-package))

  (require 'use-package)
  (setq use-package-always-ensure t)
  
#+end_src

** Environment vars
#+begin_src emacs-lisp :tangle ./init.el
  (use-package exec-path-from-shell)
  (when (memq window-system '(mac ns x))
    (exec-path-from-shell-initialize))
#+end_src
* Ivy packages

** Ivy
#+begin_src emacs-lisp :tangle ./init.el
  (use-package ivy
    :diminish
    :bind (("C-s" . swiper)
           :map ivy-minibuffer-map
           ("TAB" . ivy-alt-done)	
           ("C-l" . ivy-alt-done)
           ("C-j" . ivy-next-line)
           ("C-k" . ivy-previous-line)
           :map ivy-switch-buffer-map
           ("C-k" . ivy-previous-line)
           ("C-l" . ivy-done)
           ("C-d" . ivy-switch-buffer-kill)
           :map ivy-reverse-i-search-map
           ("C-k" . ivy-previous-line)
           ("C-d" . ivy-reverse-i-search-kill))
    :config
    (ivy-mode 1))
  
#+end_src

** Swiper
#+begin_src emacs-lisp :tangle ./init.el
  (use-package swiper)
#+end_src

** Counsel
#+begin_src emacs-lisp :tangle ./init.el
  (use-package counsel
    :bind (("M-x" . counsel-M-x)
           ("C-x b" . counsel-ibuffer)
           ("C-x C-f" . counsel-find-file)
           :map minibuffer-local-map
           ("C-r" . 'counsel-minibuffer-history)))
  
#+end_src

** Ivy-rich(better interfact for ivy)
#+begin_src emacs-lisp :tangle ./init.el
  (use-package ivy-rich
    :init
    (ivy-rich-mode 1))

#+end_src

* Cosmetics and helpful packages

** Helpers
#+begin_src emacs-lisp :tangle ./init.el
  (use-package all-the-icons)
  
  (use-package rainbow-delimiters
    :hook (prog-mode . rainbow-delimiters-mode))

  (use-package which-key
    :init (which-key-mode)
    :diminish which-key-mode
    :config
    (setq which-key-idle-delay 1))

#+end_src

** Themes(Doom themes and Doom modeline)
#+begin_src emacs-lisp :tangle ./init.el
  (use-package doom-themes
    :ensure t
    :config
    ;; Global settings (defaults)
    (setq doom-themes-enable-bold t    ; if nil, bold is universally disabled
          doom-themes-enable-italic t) ; if nil, italics is universally disabled
    (load-theme 'doom-dracula t)
    ;; Enable flashing mode-line on errors
    (doom-themes-visual-bell-config)
    ;; Enable custom neotree theme (all-the-icons must be installed!)
    (doom-themes-neotree-config)
    ;; or for treemacs users
    (setq doom-themes-treemacs-theme "doom-atom") ; use "doom-colors" for less minimal icon theme
    (doom-themes-treemacs-config)
    ;; Corrects (and improves) org-mode's native fontification.
    (doom-themes-org-config))

  (use-package doom-modeline
    :ensure t
    :init (doom-modeline-mode 1))
  
#+end_src

** Helpful
#+begin_src emacs-lisp :tangle ./init.el
  (use-package helpful
    :custom
    (counsel-describe-function-function #'helpful-callable)
    (counsel-describe-variable-function #'helpful-variable)
    :bind
    ([remap describe-function] . counsel-describe-function)
    ([remap describe-command] . helpful-command)
    ([remap describe-variable] . counsel-describe-variable)
    ([remap describe-key] . helpful-key))
  
#+end_src
* Key Bindings(General)

** General
#+begin_src emacs-lisp :tangle ./init.el
    (use-package general
      :config
      (general-create-definer my-config/leader-keys
        :keymaps '(normal insert visual emacs)
        :prefix "SPC"
        :global-prefix "C-SPC")
      (my-config/leader-keys
        "t"  '(:ignore t :which-key "toggles")
        "tt" '(counsel-load-theme :which-key "choose theme")
        "tq" 'quickrun-shell)

      (my-config/leader-keys "ts" '(hydra-text-scale/body :which-key "scale text")))

#+end_src

** Key-bindings
#+begin_src emacs-lisp :tangle ./init.el
  (global-set-key (kbd "<escape>") 'keyboard-escape-quit)

  (general-define-key (kbd "C-M-j") 'counsel-switch-buffer)
#+end_src

** Hydra
#+begin_src emacs-lisp :tangle ./init.el
  (use-package hydra)

  (defhydra hydra-text-scale (:timeout 4)
    "scale text"
    ("j" text-scale-increase "in")
    ("k" text-scale-decrease "out")
    ("f" nil "finished" :exit t))

#+end_src

* Evil-Mode

** Evil
#+begin_src emacs-lisp :tangle ./init.el
    (use-package evil
      :init
      (setq evil-want-integration t)
      (setq evil-want-keybinding nil)
      (setq evil-want-C-u-scroll t)
      (setq evil-want-C-i-jump nil)
      :config
      (evil-mode 1)
      (define-key evil-insert-state-map (kbd "C-g") 'evil-normal-state)
      (define-key evil-insert-state-map (kbd "C-h") 'evil-delete-backward-char-and-join)

      ;; Use visual line motions even outside of visual-line-mode buffers
      (evil-global-set-key 'motion "j" 'evil-next-visual-line)
      (evil-global-set-key 'motion "k" 'evil-previous-visual-line)

      (evil-set-initial-state 'messages-buffer-mode 'normal)
      (evil-set-initial-state 'dashboard-mode 'normal)
      (evil-set-initial-state 'compilation-mode 'insert))
  
#+end_src

** Disable evil for some major modes
#+begin_src emacs-lisp :tangle ./init.el
  (dolist (mode '(mastodon-mode
                  term-mode))
    (add-to-list 'evil-emacs-state-modes mode))
#+end_src
** Evil-collection
#+begin_src emacs-lisp :tangle ./init.el
  (use-package evil-collection
    :after evil
    :config
    (evil-collection-init))

#+end_src

** Evil-matchit
#+begin_src emacs-lisp :tangle ./init.el
  (use-package evil-matchit
    :hook ((web-mode
            html-mode
            mhtml-mode
            js-mode
            typescript-mode
            ) . turn-on-evil-matchit-mode))
#+end_src

** Evil-comentary
#+begin_src emacs-lisp :tangle ./init.el
  (use-package evil-commentary
    :after evil
    :config
    (evil-commentary-mode t))
#+end_src
* Project management

** Projectile
#+begin_src emacs-lisp :tangle ./init.el
  (use-package projectile
    :diminish projectile-mode
    :config
    (projectile-mode)
    (add-to-list 'projectile-globally-ignored-directories "*node_modules")
    :custom ((projectile-completion-system 'ivy))
    :bind-keymap
    ("C-c p" . projectile-command-map)
    :init
    ;; NOTE: Set this to the folder where you keep your Git repos!
    (when (file-directory-p "~/Repos")
      (setq projectile-project-search-path '("~/Repos")))
    (setq projectile-switch-project-action #'projectile-dired))

  (use-package counsel-projectile
    :config (counsel-projectile-mode))
  
#+end_src

** Magit and Forge
#+begin_src emacs-lisp :tangle ./init.el
  (use-package magit
    :custom
    (magit-display-buffer-function #'magit-display-buffer-same-window-except-diff-v1))

  ;; NOTE: Make sure to configure a GitHub token before using this package!
  ;; - https://magit.vc/manual/forge/Token-Creation.html#Token-Creation
  ;; - https://magit.vc/manual/ghub/Getting-Started.html#Getting-Started
  (use-package forge)

#+end_src

* Org-mode

** Function definition
#+begin_src emacs-lisp :tangle ./init.el
  (defun my-config/org-mode-setup ()
    (org-indent-mode)
    (variable-pitch-mode 1)
    (visual-line-mode 1))

  (defun my-config/org-font-setup ()
    ;; Replace list hyphen with dot
    (font-lock-add-keywords 'org-mode
                            '(("^ *\\([-]\\) "
                               (0 (prog1 () (compose-region (match-beginning 1) (match-end 1) "•"))))))

    ;; Set faces for heading levels
    (dolist (face '((org-level-1 . 1.2)
                    (org-level-2 . 1.1)
                    (org-level-3 . 1.05)
                    (org-level-4 . 1.0)
                    (org-level-5 . 1.1)
                    (org-level-6 . 1.1)
                    (org-level-7 . 1.1)
                    (org-level-8 . 1.1)))
      (set-face-attribute (car face) nil :font "Cantarell" :weight 'regular :height (cdr face)))

    ;; Ensure that anything that should be fixed-pitch in Org files appears that way
    (set-face-attribute 'org-block nil :foreground nil :inherit 'fixed-pitch)
    (set-face-attribute 'org-code nil   :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-table nil   :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-verbatim nil :inherit '(shadow fixed-pitch))
    (set-face-attribute 'org-special-keyword nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-meta-line nil :inherit '(font-lock-comment-face fixed-pitch))
    (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch))


  ;; Visual fill
  (defun my-config/org-mode-visual-fill ()
    (setq visual-fill-column-width 100
          visual-fill-column-center-text t)
    (visual-fill-column-mode 1))

#+end_src

** Org-mode
#+begin_src emacs-lisp :tangle ./init.el
  (use-package org
    :hook (org-mode . my-config/org-mode-setup)
    :config
    (setq org-ellipsis " ▾")
    (setq org-hide-emphasis-markers t)
    (setq org-pretty-entities t)  
    (setq org-agenda-start-with-log-mode t)
    (setq org-log-done 'time)
    (setq org-log-into-drawer t)
    (setq org-support-shift-select t)
    (setq org-agenda-files
          '("~/OrgFiles/Tasks.org"
            "~/OrgFiles/Birthdays.org"
            "~/OrgFiles/Habits.org"))

    (setq org-refile-targets
          '(("Birthdays.org" :maxlevel . 1)
            ("Tasks.org" :maxlevel . 1)))

    ;; Save Org buffers after refiling!
    (advice-add 'org-refile :after 'org-save-all-org-buffers)

    (require 'org-habit)
    (add-to-list 'org-modules 'org-habit)
    (setq org-habit-graph-column 60)

    (setq org-tag-alist
          '((:startgroup)
            ; Put mutually exclusive tags here
            (:endgroup)
            ("@errand" . ?E)
            ("@home" . ?H)
            ("@work" . ?W)
            ("agenda" . ?a)
            ("planning" . ?p)
            ("publish" . ?P)
            ("batch" . ?b)
            ("note" . ?n)
            ("idea" . ?i)))

    (setq org-todo-keywords
          '((sequence "TODO(t)" "NEXT(n)" "|" "DONE(d!)")
            (sequence "BACKLOG(b)" "PLAN(p)" "READY(r)" "ACTIVE(a)" "REVIEW(v)" "WAIT(w@/!)" "HOLD(h)" "|" "COMPLETED(c)" "CANC(k@)")))

    (setq org-agenda-custom-commands
          '(("d" "Dashboard"
             ((agenda "" ((org-deadline-warning-days 7)))
              (todo "NEXT"
                    ((org-agenda-overriding-header "Next Tasks")))
              (tags-todo "agenda/ACTIVE" ((org-agenda-overriding-header "Active Projects")))))
            ("n" "Next Tasks"
             ((todo "NEXT"
                    ((org-agenda-overriding-header "Next Tasks")))))
            ("W" "Work Tasks" tags-todo "+work-email")

      ;; Low-effort next actions
            ("e" tags-todo "+TODO=\"NEXT\"+Effort<15&+Effort>0"
             ((org-agenda-overriding-header "Low Effort Tasks")
              (org-agenda-max-todos 20)
              (org-agenda-files org-agenda-files)))

            ("w" "Workflow Status"
             ((todo "WAIT"
                    ((org-agenda-overriding-header "Waiting on External")
                     (org-agenda-files org-agenda-files)))
              (todo "REVIEW"
                    ((org-agenda-overriding-header "In Review")
                     (org-agenda-files org-agenda-files)))
              (todo "PLAN"
                    ((org-agenda-overriding-header "In Planning")
                     (org-agenda-todo-list-sublevels nil)
                     (org-agenda-files org-agenda-files)))
              (todo "BACKLOG"
                    ((org-agenda-overriding-header "Project Backlog")
                     (org-agenda-todo-list-sublevels nil)
                     (org-agenda-files org-agenda-files)))
              (todo "READY"
                    ((org-agenda-overriding-header "Ready for Work")
                     (org-agenda-files org-agenda-files)))
              (todo "ACTIVE"
                    ((org-agenda-overriding-header "Active Projects")
                     (org-agenda-files org-agenda-files)))
              (todo "COMPLETED"
                    ((org-agenda-overriding-header "Completed Projects")
                     (org-agenda-files org-agenda-files)))
              (todo "CANC"
                    ((org-agenda-overriding-header "Cancelled Projects")
                     (org-agenda-files org-agenda-files)))))))

    (setq org-capture-templates
          `(("t" "Tasks / Projects")
            ("tt" "Task" entry (file+olp "~/OrgFiles/Tasks.org" "Inbox")
             "* TODO %?\n  %U\n  %a\n  %i" :empty-lines 1)

            ("j" "Journal Entries")
            ("jj" "Journal" entry
             (file+olp+datetree "~/OrgFiles/Journal.org")
             "\n* %<%I:%M %p> - Journal :journal:\n\n%?\n\n"
             :clock-in :clock-resume
             :empty-lines 1)
            ("jm" "Meeting" entry
             (file+olp+datetree "~/OrgFiles/Journal.org")
             "* %<%I:%M %p> - %a :meetings:\n\n%?\n\n"
             :clock-in :clock-resume
             :empty-lines 1)

            ("w" "Workflows")
            ("we" "Checking Email" entry (file+olp+datetree "~/OrgFiles/Journal.org")
             "* Checking Email :email:\n\n%?" :clock-in :clock-resume :empty-lines 1)

            ("m" "Metrics Capture")
            ("mw" "Weight" table-line (file+headline "~/OrgFiles/Metrics.org" "Weight")
             "| %U | %^{Weight} | %^{Notes} |" :kill-buffer t)))

    (define-key global-map (kbd "C-c j")
      (lambda () (interactive) (org-capture nil "jj")))

    (my-config/org-font-setup))
  
#+end_src

** Org-bullets
#+begin_src emacs-lisp :tangle ./init.el
  (use-package org-bullets
    :after org
    :hook (org-mode . org-bullets-mode)
    :custom
    (org-bullets-bullet-list '("◉" "○" "●" "○" "●" "○" "●")))
 
#+end_src

** Visual fill
#+begin_src emacs-lisp :tangle ./init.el
  (use-package visual-fill-column
    :hook (org-mode . my-config/org-mode-visual-fill))
  
#+end_src

* Org-babel

** Org-babel
#+begin_src emacs-lisp :tangle ./init.el
    (org-babel-do-load-languages
   'org-babel-load-languages
   '((emacs-lisp . t)
     (python . t)))

  (push '("conf-unix" . conf-unix) org-src-lang-modes)

  ;; Automatically tangle our Emacs.org config file when we save it
  (defun my-config/org-babel-tangle-config ()
    (when (string-equal (buffer-file-name)
                        (expand-file-name "~/.emacs.d/init.el"))
      ;; Dynamic scoping to the rescue
      (let ((org-confirm-babel-evaluate nil))
      (org-babel-tangle))))

  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'my-config/org-babel-tangle-config)))
#+end_src

** Org-tempo
#+begin_src emacs-lisp :tangle ./init.el
  (require 'org-tempo)
  (add-to-list 'org-structure-template-alist '("sh" . "src shell"))
  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
  (add-to-list 'org-structure-template-alist '("py" . "src python"))
#+end_src

* Org-Roam

** Org-Roam
#+begin_src emacs-lisp :tangle ./init.el
  (use-package org-roam
    :ensure t
    :custom
    (org-roam-directory "~/Repos/RoamNotes")
    (org-roam-completion-everywhere t)
    (org-roam-node-display-template
     (concat "${title:*} "
             (propertize "${tags:15}" 'face 'org-tag)))
    (org-roam-capture-templates
     '(("d" "default" plain
        "%?"
        :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
        :unnarrowed t)
       ("c" "command" plain
        "* Command: *${title}*\n\n- *Description*:  %?\n\n- *Usage*: \n\n- *Useful Options*: \n\n- *Examples*: \n\n"
        :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
        :unnarrowed t)
       ("m" "math" plain
        "* *${title}*\n\n- *Definition*:  %?\n\n"
        :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n#+filetags: mathematics")
        :unnarrowed t)
       ("b" "book notes" plain
        "\n* Source\n\nAuthor: %^{Author}\nTitle: ${title}\nYear: %^{Year}\n\n* Summary\n\n%?"
        :if-new (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n#+date: %U\n")
        :unnarrowed t)))
    :bind (("C-c n l" . org-roam-buffer-toggle)
           ("C-c n f" . org-roam-node-find)
           ("C-c n i" . org-roam-node-insert)
           :map org-mode-map
           ("C-M-i" . completion-at-point))
    :config
    (org-roam-setup))
  
#+end_src

* LSP-mode

** LSP-mode
#+begin_src emacs-lisp :tangle ./init.el
  (defun efs/lsp-mode-setup ()
    (setq lsp-headerline-breadcrumb-segments '(path-up-to-project file symbols))
    (lsp-headerline-breadcrumb-mode))

  (use-package lsp-mode
    :commands (lsp lsp-deferred)
    :hook (lsp-mode . efs/lsp-mode-setup)
    :init
    (setq lsp-keymap-prefix "C-c l")  ;; Or 'C-l', 's-l'
    :config
    (lsp-enable-which-key-integration t))
#+end_src

** LSP-ivy
#+begin_src emacs-lisp :tangle ./init.el
  (use-package lsp-ivy
    :commands lsp-ivy-workspace-symbol)
#+end_src

** LSP-treemacs
#+begin_src emacs-lisp :tangle ./init.el
  (use-package lsp-treemacs
    :commands lsp-treemacs-errors-list)
#+end_src

** LSP-ui
#+begin_src emacs-lisp :tangle ./init.el
  (use-package lsp-ui
    :hook (lsp-mode . lsp-ui-mode)
    :custom
    (lsp-ui-doc-position 'bottom))
#+end_src

** Languages
#+begin_src emacs-lisp :tangle ./init.el
  (use-package typescript-mode
    :mode "\\.ts\\'" "\\.tsx\\'"
    :hook (typescript-mode . lsp-deferred)
    :config
    (setq typescript-indent-level 2))

  (use-package rjsx-mode
    :hook (rjsx-mode . lsp-deferred)
    :config
    (add-to-list 'auto-mode-alist '("components\\/.*\\.js\\'" . rjsx-mode)))

  (use-package nim-mode
    :mode "\\.nim\\'"
    :hook (nim-mode . lsp))

  (use-package lua-mode
    :mode "\\.lua\\'")

  (use-package yaml-mode
    :mode "\\.yml\\'")

#+end_src
** Servers
#+begin_src emacs-lisp :tangle ./init.el
    (use-package lsp-pyright
    :ensure t
    :hook (python-mode . (lambda () (require 'lsp-pyright) (lsp-deferred))))

    (use-package lsp-java
      :hook (java-mode . (lambda () (require 'lsp-java) (lsp-deferred))))
#+end_src

** Company
#+begin_src emacs-lisp :tangle ./init.el
  (use-package company
    :after lsp-mode
    :hook (lsp-mode . company-mode)
    :bind (:map company-active-map
           ("<tab>" . company-complete-selection))
          (:map lsp-mode-map
           ("<tab>" . company-indent-or-complete-common))
    :custom
    (company-minimum-prefix-length 1)
    (company-idle-delay 0.0))

  (use-package company-box
    :hook (company-mode . company-box-mode))
#+end_src

** flycheck
#+begin_src emacs-lisp :tangle ./init.el
  (use-package flycheck)
#+end_src
** Quickrun
#+begin_src emacs-lisp :tangle ./init.el
  (use-package quickrun)
#+end_src
** Additional packages
#+begin_src emacs-lisp :tangle ./init.el
  (use-package auto-virtualenv
  :ensure t
  :init
  (use-package pyvenv
    :ensure t)
  :config
  (add-hook 'python-mode-hook 'auto-virtualenv-set-virtualenv)
  (add-hook 'projectile-after-switch-project-hook 'auto-virtualenv-set-virtualenv)  ;; If using projectile
  )
#+end_src
* Mastodon packages

** Discovery
#+begin_src emacs-lisp :tangle ./init.el
  (use-package discover)
#+end_src
** Mastodon
#+begin_src emacs-lisp :tangle ./init.el
    (use-package mastodon
      :ensure t
      :config
      (setq mastodon-instance-url "https://social.linux.pizza/"
            mastodon-active-user "rubberduck")
      (mastodon-discover))
#+end_src
